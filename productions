Program ->
  FunctionLoop


FunctionLoop ->
  FunctionLoop Function
| epsilon


Function ->
  FunctionDec Params Locals Body


FunctionDec->
  FUNCTION IDENTIFIER Semicolon


Params ->
  BEGINPARAMS DecLoop ENDPARAMS


Locals ->
  BEGINLOCALS DecLoop ENDLOCALS


Body ->
  BEGINBODY StatementLoop ENDBODY


DecLoop ->
  DecLoop Declaration Semicolon
| epsilon


StatementLoop ->
  StatementLoop Statement Semicolon
| Statement Semicolon

Declaration ->
  IdentifierLoop COLON Declaration_ INTEGER

Declaration_ ->
  ARRAY L_SQUARE_BRACKET NUMBER R_SQUARE_BRACKET OF
| epsilon


IdentifierLoop ->
  IdentifierLoop COMMA IDENTIFIER
| IDENTIFIER


Statement ->
  Assignment
| IfStatement
| WhileLoop
| DoWhile
| READ VarLoop
| WRITE VarLoop
| CONTINUE
| RETURN Expression


Assignment ->
  Var ASSIGN Expression



IfStatement ->
  IF BoolExpr THEN StatementLoop OptElse ENDIF


OptElse ->
  ELSE StatementLoop
| epsilon



WhileLoop ->
  WHILE BoolExpr BEGINLOOP StatementLoop ENDLOOP



DoWhile ->
  DO BEGINLOOP StatementLoop ENDLOOP WHILE BoolExpr



VarLoop ->
  VarLoop COMMA Var
| Var



BoolExpr ->
  BoolExpr OR RelationAndExpr
| RelationAndExpr




RelationAndExpr ->
  RelationAndExpr AND RelationExpr
| RelationExpr



RelationExpr ->
  RelationExpr_
| NOT RelationExpr_


RelationExpr_ ->
  Expression Comp Expression
| TRUE
| FALSE
| L_PAREN BoolExpr R_PAREN



Comp ->
  EQ
| NEQ
| LT
| GT
| LTE
| GTE



Expression ->
  Expression AddSub MultiplicativeExpr
| MultiplicativeExpr


AddSub ->
  ADD
| SUB



MultiplicativeExpr ->
  MultiplicativeExpr MultOP Term
| Term


MultOP ->
  MULT
| DIV
| MOD



Term ->
  Term_
| SUB Term_
| IDENTIFIER L_PAREN Term__ R_PAREN


Term_ ->
  Var
| NUMBER
| L_PAREN Expression R_PAREN


Term__ ->
  ExpressionLoop
| epsilon


ExpressionLoop ->
  ExpressionLoop COMMA Expression
| Expression


Var ->
  IDENTIFIER L_SQUARE_BRACKET Expression R_SQUARE_BRACKET
| IDENTIFIER


Semicolon->
  SEMICOLON
